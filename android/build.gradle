// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = findProperty('android.buildToolsVersion') ?: '33.0.0'
        // had to change minSdkVersion from 21 to 24 to compile @google-cloud/recaptcha-enterprise-react-native
        // From here https://github.com/invertase/react-native-firebase/discussions/7489
        minSdkVersion = Integer.parseInt(findProperty('android.minSdkVersion') ?: '24')
        compileSdkVersion = Integer.parseInt(findProperty('android.compileSdkVersion') ?: '33')
        targetSdkVersion = Integer.parseInt(findProperty('android.targetSdkVersion') ?: '33')
        kotlinVersion = findProperty('android.kotlinVersion') ?: '1.8.10'
        frescoVersion = findProperty('expo.frescoVersion') ?: '2.5.0'

        // We use NDK 23 which has both M1 support and is the side-by-side NDK version from AGP.
        ndkVersion = "23.1.7779620"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath('com.android.tools.build:gradle:7.4.2')
        classpath('com.facebook.react:react-native-gradle-plugin')
    }
    ////////////////////// CoinJar Addition ////////////////////////
    // Added to compile eml react native sdk
    subprojects { subproject ->
        afterEvaluate{
          if((subproject.plugins.hasPlugin('android') || subproject.plugins.hasPlugin('android-library'))) {
              android {
                  compileSdkVersion rootProject.ext.compileSdkVersion
                  buildToolsVersion rootProject.ext.buildToolsVersion
              }
          }
      }
    }
    ///////////////////////////////////////////////////////////////
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url(new File(['node', '--print', "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim(), '../android'))
        }
        maven {
            // Android JSC is installed from npm
            url(new File(['node', '--print', "require.resolve('jsc-android/package.json')"].execute(null, rootDir).text.trim(), '../dist'))
        }

        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }

        /////////// CoinJar Addition ////////////////////////
        maven {
            url = 'https://seventh-beam.mycloudrepo.io/repositories/eml'
            credentials {
                username System.getenv("EML_USER_NAME")
                password System.getenv("EML_PASSWORD")
            }
        }
        maven { url 'https://zendesk.jfrog.io/zendesk/repo' }
        //////////////////////////////////////////////////
    }
}
